plugins {
	id "java"
	id "application"
	id "com.palantir.graal" version "0.6.0-43-gd59c8dc"
}

repositories {
	maven {
		url = "https://oss.sonatype.org/content/repositories/snapshots/"
	}
	mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
mainClassName = "com.mageddo.graalvm.okhttp.Application"

task debug(type: JavaExec){
	debug = true
	classpath = sourceSets.main.runtimeClasspath
	main = mainClassName
}

graal {
	mainClass mainClassName
	graalVersion(graalVMVersion)
	outputName(project.name)
	option("--no-server")
}

dependencies {

	compile("org.slf4j:slf4j-simple:1.7.29")
//	compile("ch.qos.logback:logback-classic:1.2.3")
	compile("com.squareup.okhttp3:okhttp:4.3.0-20191205.122911-19")

}

task copyToLib(type: Copy) {
	into "$buildDir/libs/lib"
	from configurations.runtime
}

task fatJar(type: Jar, dependsOn: copyToLib) {
	archiveBaseName = "${project.name}-all"
	manifest {
		attributes(
			'Main-Class': mainClassName,
			'Class-Path': configurations.runtime.collect { "lib/${it.name}" }.join(' ')
		)
	}
	with jar
}
