buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}
plugins {
  id "java"
  id "com.palantir.graal" version "0.12.0"
}

repositories {
  mavenLocal()
  jcenter()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def className = "com.mageddo.Application"

task debug(type: JavaExec) {
  debug = true
  classpath = sourceSets.main.runtimeClasspath
  main = className
}

task run(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = className
}

graal {
  mainClass className
  graalVersion(graalVMVersion)
  outputName(project.name)
  javaVersion("17")
  option("-H:IncludeResources=.*.yml")
  option("--allow-incomplete-classpath")
  option("-H:ReflectionConfigurationFiles=${project.buildDir}/reflect.json")
  option("-H:+AllowVMInspection")
  option("-H:+ReportUnsupportedElementsAtRuntime")
  option("-H:EnableURLProtocols=http,https")
  option("--enable-all-security-services")
  option("-Dfile.encoding=UTF-8")
  option("--no-server")
  option("-J-Xmx5G")
}

dependencies {

  compileOnly 'org.projectlombok:lombok:1.18.24'
  annotationProcessor 'org.projectlombok:lombok:1.18.24'
  compileOnly("com.mageddo.nativeimage:reflection-config-generator:2.3.4")
  annotationProcessor("com.mageddo.nativeimage:reflection-config-generator:2.3.4")

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.1'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

  testCompileOnly 'org.projectlombok:lombok:1.18.24'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
  implementation  "ch.qos.logback:logback-classic:1.2.3"
  testImplementation "junit:junit:4.12"

}

task fatJar(type: Jar) {
  archiveBaseName = project.name + '-all'
  manifest {
    attributes(
      'Main-Class': className,
      'Class-Path': configurations.compileClasspath.collect { "lib/${it.name}" }.join(' ')
    )
  }
  from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

